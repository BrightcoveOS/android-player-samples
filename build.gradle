// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath 'com.android.tools.build:gradle:1.2.+'
    classpath 'de.undercouch:gradle-download-task:0.+'
  }
}

println "Using Android Native Player version: $anpVersion"

project.ext {
  // The directory that will be used to reference dependent libraries.
  // Modify as needed. Defaulted to users home directory.
  libraryDirectory = "${System.getProperty("user.home")}/libs/"
  imaAndroidSdkJarFileName = "ima-android-sdk-beta9.jar"
}

// Automatically download the Google IMA third party zip file and
// extract the jar to satisfy the dependency.
def downloadIMA() {
  def libsdk = libraryDirectory + imaAndroidSdkJarFileName

  // Conditionally download the Google IMA SDK during
  // configuration.  This step is done once.  The 'clean' task will not
  // remove libraries from /libs/.
  File dep = file(libsdk)
  if (!dep.exists()) {
    apply plugin: 'download-task'

    println "Downloading the Google IMA SDK..."

    download {
      src 'https://dl.google.com/in-stream/google_sdk/android/v3/' + imaAndroidSdkJarFileName
      dest file(libraryDirectory + imaAndroidSdkJarFileName)
    }
  }
}

configure(subprojects.findAll {it.name.endsWith('SampleApp')}) {
  apply plugin: 'com.android.application'

  repositories {
    mavenLocal()
    jcenter()

    maven {
      url 'http://repo.brightcove.com/snapshots'
    }
    maven {
      url 'http://repo.brightcove.com/releases'
    }
    maven {
      url System.getenv('ANDROID_HOME') + '/extras/android/m2repository'
    }
    maven {
      url System.getenv('ANDROID_HOME') + '/extras/google/m2repository'
    }

    // This top level file repository will provide/contain third party
    // dependencies which are either downloaded directly (not using a
    // Maven repo) or installed manually for something like VisualOn.
    flatDir {
      dirs libraryDirectory
    }
  }

  android {
    compileSdkVersion 21
    buildToolsVersion "20.0.0"

    defaultConfig {
      minSdkVersion 10
      targetSdkVersion 21
    }

    signingConfigs {
      release {
          storeFile file("${rootDir}/release.keystore")
          storePassword "my_keystore_password"
          keyAlias "my_key"
          keyPassword "my_password"
      }
    }

    buildTypes {
      release {
        signingConfig signingConfigs.release
        minifyEnabled true
        proguardFiles getDefaultProguardFile('proguard-android.txt'), "${rootDir}/proguard-project.txt"
      }
    }
  }

  dependencies {
    compile "com.brightcove.player:android-sdk:${anpVersion}"
  }

  if (project.name.contains('AdobePass')) {
    dependencies {
      compile files(libraryDirectory + "android_accessenabler-1.7.2.jar")
      compile 'com.google.code.gson:gson:2.+'
    }
  }

  if (project.name.contains('AIS')) {
    dependencies {
      compile 'com.google.code.gson:gson:2.+'
    }
  }

  if (project.name.contains('IMA')) {
    downloadIMA()
    dependencies {
      compile "com.brightcove.player:android-ima-plugin:${anpVersion}"
      compile files(libraryDirectory + imaAndroidSdkJarFileName)
      compile 'com.google.android.gms:play-services:6.5.87'
    }
  }

  if (project.name.contains('Omniture')) {
    dependencies {
      compile "com.brightcove.player:android-omniture-plugin:${anpVersion}"
      compile files(libraryDirectory + "adobeMobileLibrary.jar")
    }

    android {
      defaultConfig {
        minSdkVersion 16
      }
    }
  }

  if (project.name.contains('OnceUx')) {
    dependencies {
      compile "com.brightcove.player:android-onceux-plugin:${anpVersion}"
    }
  }

  if (project.name.contains('Widevine')) {
    dependencies {
      compile "com.brightcove.player:android-widevine-plugin:${anpVersion}"
    }

    android {
      defaultConfig {
        minSdkVersion 11
      }
    }
  }

  if (project.parent.name.equals('brightcove-hls')) {
    dependencies {
      compile "com.brightcove.player:android-hls-player:${anpVersion}"
    }
  }

  if (project.parent.name.equals('brightcove-exoplayer')) {
    dependencies {
      compile "com.brightcove.player:exoplayer:${anpVersion}"
    }

    android {
      defaultConfig {
        minSdkVersion 16
      }
    }
  }

  if (project.name.contains('WebVTT')) {
    android {
      defaultConfig {
        minSdkVersion 19
      }
    }
  }

  if (project.name.contains('WebView')) {
    android {
      lintOptions {
        disable 'SetJavaScriptEnabled'
      }
    }
  }

  if (project.name.contains('Cast')) {
    dependencies {
      compile "com.brightcove.player:android-cast-plugin:${anpVersion}"
      compile 'com.google.sample.castcompanionlibrary:CastCompanionLibrary-android:1.+'
      compile 'com.google.android.gms:play-services:6.5.87'
    }
  }

  if (project.name.contains('FreeWheel')) {
    dependencies {
      compile "com.brightcove.player:android-freewheel-plugin:${anpVersion}"
      compile files(libraryDirectory + "AdManager.jar")
    }
  }

  println "Processing ${project.name}"
}
